apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.tencent.liteav.demo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 24
        versionName "9.5.0.1001"

        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a','arm64-v8a'
        }
        buildConfigField("boolean", "BUILD_TRTC", "${buildTRTC}")
        buildConfigField("boolean", "BUILD_UGC", "${buildUGC}")
        buildConfigField("boolean", "BUILD_PLAYER", "${buildPlayer}")
        buildConfigField("boolean", "BUILD_LIVE", "${buildLive}")
        buildConfigField("boolean", "BUILD_SMART", "${buildSmart}")
        buildConfigField("boolean", "BUILD_INTERNATIONAL", "${buildInternational}")
        buildConfigField("boolean", "BUILD_REFACTORING_LIVE", "${buildRefactoringLive}")
        buildConfigField("boolean", "BUILD_FROM_DEVOPS", "${buildFromDevops}")
    }

    signingConfigs {
        release {
            storeFile file("trtc.keystore")
            storePassword "tencent.com"
            keyAlias "tencent"
            keyPassword "tencent.com"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libsoundtouch.so'
        pickFirst '**/libliteavsdk.so'
        pickFirst '**/libtxsoundtouch.so'
        doNotStrip "*/armeabi/libYTCommon.so"
        doNotStrip "*/armeabi-v7a/libYTCommon.so"
        doNotStrip "*/x86/libYTCommon.so"
        doNotStrip "*/arm64-v8a/libYTCommon.so"
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')
    implementation rootProject.ext.liteavSdk

    if (buildTRTC.toBoolean()) {
        implementation project(':trtcdemo')
    }

    if (buildLive.toBoolean()) {
        implementation project(':v1liveplayerdemo')
        implementation project(':v1livepusherdemo')
        implementation project(':lebplayerdemo')
        implementation project(':livepusherdemo')
        implementation project(':liveplayerdemo')
        implementation project(':livelinkmicdemoold')
        implementation project(':livelinkmicdemonew')
        implementation project(':livepusherscreendemo')
    }

    if (buildSmart.toBoolean()) {
        implementation project(':livepusherdemo')
        implementation project(':liveplayerdemo')
        implementation project(':livelinkmicdemoold')
        implementation project(':livepusherscreendemo')
    }

    if (buildUGC.toBoolean()) {
        implementation project(':ugcvideoeditdemo')
        implementation project(':ugcvideojoindemo')
        implementation project(':ugcvideorecorddemo')
        implementation project(':ugcvideouploaddemo')
    }

    if (buildPlayer.toBoolean()) {
        implementation project(':superplayerdemo')
    }

    if (buildInternational.toBoolean()) {
        implementation project(':livelinkmicdemonew')
    }

    //TODO:临时增加重构live版本
    if (buildRefactoringLive.toBoolean()) {
        implementation project(':liveplayerdemo')
        implementation project(':v1liveplayerdemo')
        implementation project(':lebplayerdemo')
    }

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.code.gson:gson:2.3.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation('com.blankj:utilcode:1.25.9', {
        exclude group: 'com.google.code.gson', module: 'gson'
    })
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    releaseImplementation project(':loginnetwork')
    debugImplementation project(':loginmock')
}