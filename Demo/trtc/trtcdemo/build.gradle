apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk {
            moduleName "CustomerCrypt"
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild{
        cmake{
            path 'src/main/jni/CMakeLists.txt'
        }
    }

    sourceSets {
        main {
            java {
                if (!buildPlayer.toBoolean()) {
                    exclude 'com/tencent/liteav/trtcdemo/ui/fragment/VodPlayerFragment.java'
                }
                if (!buildLive.toBoolean()) {
                    exclude 'com/tencent/liteav/trtcdemo/model/manager/chorus/TRTCChorusManagerImpl.java'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.liteavSdk
    implementation(rootProject.ext.imSdk, {
        exclude group: 'com.google.code.gson', module: 'gson'
    })
    implementation project(':debug')
    implementation project(':common')
    implementation project(':beautysettingkit')
    implementation project(':audioeffectsettingkit')
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'me.dm7.barcodescanner:zxing:1.8.4'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation('com.blankj:utilcode:1.25.9', {
        exclude group: 'com.google.code.gson', module: 'gson'
    })
}
